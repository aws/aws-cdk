#
#####################################
##      GUARD HOOKS RULE SET      ##
#####################################
# Rule: NO_ROOT_PRINCIPALS_EXCEPT_KMS_SECRETS
# Description: Prevents use of :root principals in AWS resources except for KMS Keys and Secrets Manager.
# This rule is designed for CFN Guard Hooks to catch resolved intrinsic functions.

let resources_no_root_principals = Resources.*[
  Type != 'AWS::KMS::Key'
  Type != 'AWS::SecretsManager::Secret'
  Metadata.guard.SuppressedRules not exists or
  Metadata.guard.SuppressedRules.* != "NO_ROOT_PRINCIPALS_EXCEPT_KMS_SECRETS"
]

rule NO_ROOT_PRINCIPALS_EXCEPT_KMS_SECRETS when %resources_no_root_principals !empty {
    %resources_no_root_principals {
        # Check AssumeRolePolicyDocument (IAM Roles)
        when Properties.AssumeRolePolicyDocument exists {
            Properties.AssumeRolePolicyDocument.Statement[*] {
                when Effect == "Allow" {
                    when Principal.AWS exists {
                        when Principal.AWS is_string {
                            Principal.AWS != /(?i):root$/
                        }
                        # Check if AWS is an array - only check string items
                        when Principal.AWS is_list {
                            when Principal.AWS[*] is_string {
                                Principal.AWS[*] != /(?i):root$/
                            }
                        }
                    }
                }
            }
        }
        
        # Check PolicyDocument (IAM Policies, S3 Bucket Policies, etc.)
        when Properties.PolicyDocument exists {
            Properties.PolicyDocument.Statement[*] {
                when Effect == "Allow" {
                    when Principal.AWS exists {
                        when Principal.AWS is_string {
                            Principal.AWS != /(?i):root$/
                        }
                        when Principal.AWS is_list {
                            Principal.AWS[*] != /(?i):root$/
                        }
                    }
                }
            }
        }
        
        # Check Policy (various services)
        when Properties.Policy exists {
            Properties.Policy.Statement[*] {
                when Effect == "Allow" {
                    when Principal.AWS exists {
                        when Principal.AWS is_string {
                            Principal.AWS != /(?i):root$/
                        }
                        when Principal.AWS is_list {
                            Principal.AWS[*] != /(?i):root$/
                        }
                    }
                }
            }
        }
        
        # Check ResourcePolicy (Lambda, SNS, SQS, etc.)
        when Properties.ResourcePolicy exists {
            Properties.ResourcePolicy.Statement[*] {
                when Effect == "Allow" {
                    when Principal.AWS exists {
                        when Principal.AWS is_string {
                            Principal.AWS != /(?i):root$/
                        }
                        when Principal.AWS is_list {
                            Principal.AWS[*] != /(?i):root$/
                        }
                    }
                }
            }
        }
    }
}